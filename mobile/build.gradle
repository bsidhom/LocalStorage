apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        // Minimum of 26 is required for java.nio.file.Files.
        minSdkVersion 26
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 9
        versionName "2.3"

        def documentsAuthorityValue = "com.ianhanniballake.localstorage.documents"
        manifestPlaceholders = [documentsAuthority: documentsAuthorityValue]
        buildConfigField "String", "DOCUMENTS_AUTHORITY", "\"${documentsAuthorityValue}\""
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("LOCAL_STORAGE_ALIAS")
            keyPassword System.getenv("LOCAL_STORAGE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    def appcompat_version = "1.1.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.core:core-ktx:${rootProject.ext.coreVersion}"
    //implementation "com.android.support:appcompat-v7:28.0.0-rc01"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"
    implementation "com.android.billingclient:billing:1.1"
}
